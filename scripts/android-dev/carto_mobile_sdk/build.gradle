apply plugin: 'com.android.library'

// IMPORTANT!
// This variable should normally reflect the value of '--profile' argument when generating wrappers via 'swigpp-java.py' script!
def profiles = "standard+valhalla"

def cmakeOptions = {
    def jsonFile = file('$projectDir/../../../build/sdk_profiles.json')
    def parsedJson = new groovy.json.JsonSlurper().parseText(jsonFile.text)

    def profileList = profiles.split('\\+')
    if (!profileList.contains('lite')) {
        profileList += ['standard'];
    }

    def options = []
    def defines = ''
    def includes = [] as Set
    def excludes = [] as Set
    for (profile in profileList) {
        if (!parsedJson.containsKey(profile)) {
            throw new java.lang.Exception("The profile ${profile} is not valid!")
        }
        if (parsedJson[profile].containsKey('cmake-options')) {
            for (option in parsedJson[profile]['cmake-options'].split(';')) {
                options += ['-D' + option]
            }
        }
        if (parsedJson[profile].containsKey('cmake-includes')) {
            for (include in parsedJson[profile]['cmake-includes'].split(';')) {
                includes += [include]
            }
        }
        if (parsedJson[profile].containsKey('cmake-excludes')) {
            for (exclude in parsedJson[profile]['cmake-excludes'].split(';')) {
                excludes += [exclude]
            }
        }
        for (include in includes) {
            if (include != "") {
                options += ["-DINCLUDE_$include:BOOL=ON"]
            }
        }
        for (exclude in excludes) {
            if (exclude != "" && !includes.contains(exclude)) {
                options += ["-DINCLUDE_$exclude:BOOL=OFF"]
            }
        }
        if (parsedJson[profile].containsKey('defines')) {
            for (define in parsedJson[profile]['defines'].split(';')) {
                defines += '-D' + define + ' '
            }
        }
    }

    options + ['-DSDK_CPP_DEFINES=' + defines.trim()]
}

android {
    namespace 'com.carto'
    compileSdkVersion 33

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
        ndk {
            abiFilters 'arm64-v8a', 'x86_64'
            abiFilters 'x86', 'armeabi-v7a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_STL=c++_static", "-DWRAPPER_DIR=${projectDir.absolutePath.replaceAll('\\\\', '/')}/../../../generated/android-java/wrappers"
                cmakeOptions().each {option -> arguments option }
            }
        }
    }

    externalNativeBuild {
        cmake {
            path file("$projectDir/../../build/CMakeLists.txt")
        }
    }

    sourceSets {
        main {
            java.srcDirs = ["$projectDir/../../../generated/android-java/proxies", "$projectDir/../../../android/java"]
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
